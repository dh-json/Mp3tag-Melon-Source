# Mp3tag parsing for Melon.com
# Path: %appdata%\Mp3tag\data\sources\
# Originated by Hyosang Park http://hyosang.kr

[Include]=Melon#.inc
[IndexUrl]=https://www.melon.com/search/song/index.htm?&q=
[AlbumUrl]=https://www.melon.com/song/detail.htm?songId=
[IndexFormat]=%_url%|%Title%|%Artist%|%_preview%|%Album%
[SearchBy]=%Title% %ARTIST%

[ParserScriptIndex]=...
# ###################################################################
# This script parses the webpage for all releases
# by this albumname.
#
# The output format is defined by IndexFormat above, which
# is used by the selection dialog
# ###################################################################
debug "on" "debug_melon_parse.out"
debugWriteInput "debug-input_melon_parse.out"

FindLine "<tbody>"

Do
  FindLine "<div class=\"ellipsis"
  MoveLine 1
  Unspace
  if "<button type=\"button\" disabled=\"disabled"
    Set "Unplayble" "True"
  endif

  # _url
  FindLine "melon.link.goSongDetail"
  Unspace
  FindInLine "melon.link.goSongDetail('"
  SayUntil "');"
  Say "|"

  ifoutput "Unplayble"
    # Unplayble
    FindLine "<span title"
  else
    # Playble
    FindLine "melon.play.playSong"
  endif

  # Title
  Unspace
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"

  # Artist
  FindLine "<div id=\"artistName"
  MoveLine 1
  Unspace
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"

  # _preview
  FindLine "<div class=\"ellipsis"
  FindLine "melon.link.goAlbumDetail"
  Say "https://www.melon.com/album/detail.htm?albumId="
  Unspace
  FindInLine "melon.link.goAlbumDetail('"
  SayUntil "');"
  Say "|"

  # Album
  FindLine "melon.link.goAlbumDetail("
  Unspace
  RegexpReplace "<[^>]+>" ""
  SayRest
  SayNewLine

  FindLine "<tr " 1 1
  Unspace
While "<tr "

[ParserScriptAlbum]=...
# ###################################################################
# This script parses the albums page for all common information.
#
# The current output variable is set via the outputto command
# All these fields are used by the confirm online information dialog
# ###################################################################
#debug "on" "debug_melon_set2.out"
#debugWriteInput "debug-input_melon_set2.out"

# CoverArt
OutputTo "coverurl"
FindLine "<meta property=\"og:image\""
Unspace

# ChangeSize
RegexpReplace "(_)(\d+)(.jpg+)" "_1000.jpg"
RegexpReplace "(\w+\.\w+)\?.*" "$1"
FindInLine "content=\""
SayUntil "\""

# Title
OutputTo "Title"
FindLine "<strong class=\"none\">"
MoveLine 5
Unspace
SayRest

# Artist
OutputTo "Artist"
FindLine "<div class=\""
FindInLine "<div class=\""
MoveLine 2
Unspace
RegexpReplace "<[^>]+>" ""
SayRest

# Album
OutputTo "Album"
FindLine "<dt>앨범</dt>"
MoveLine 1
Unspace
RegexpReplace "<[^>]+>" ""
SayRest

OutputTo "Year"
FindLine "<dt>발매일</dt>"
MoveLine 1
Unspace
# Year
RegexpReplace "<[^>]+>" ""
SayUntil "."
# Date
#RegexpReplace "[^0-9]" ""
#SayRest

# Genre
OutputTo "Genre"
FindLine "<dt>장르</dt>"
MoveLine 1
Unspace
RegexpReplace "<[^>]+>" ""
SayRest

# Lyrics
FindLine "<div class=\"wrap_lyric\" id=\"lyricArea\">"
MoveLine 1
FindLine "<div"
Unspace
IfNot "<div class="lyric_none">"
  OutputTo "Unsyncedlyrics"
  MoveLine 1
  RegexpReplace "<[^>]+>" "\r\n"
  SayRest
endif

# Lyricist
OutputTo "Lyricist"
FindLine "작사가 정보" 1 1
FindLine "<li>" 1 1
Do
  FindLine "<div class=\"ellipsis artist" 1 1
  FindLine "melon.link.goArtistDetail" 1 1
  RegexpReplace "<[^>]+>" ""
  SayRest
  FindLine "</li>" 1 1
  MoveLine 2 1 1
  Unspace

  If "<li>"
    Say ", "
  endif
While "<li>"

# Composer
OutputTo "Composer"
FindLine "작곡가 정보" 1 1
FindLine "<li>" 1 1
Do
  FindLine "<div class=\"ellipsis artist" 1 1
  FindLine "melon.link.goArtistDetail" 1 1
  RegexpReplace "<[^>]+>" ""
  SayRest
  FindLine "</li>" 1 1
  MoveLine 2 1 1
  Unspace

  If "<li>"
    Say ", "
  endif
While "<li>"

Set "Unplayble" ""
