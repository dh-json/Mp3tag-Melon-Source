[Include]=Melon#.inc
[IndexUrl]=https://www.melon.com/search/album/index.htm?section=all&searchGnbYn=Y&kkoSpl=N&kkoDpType=&q=
[AlbumUrl]=https://www.melon.com/album/detail.htm?albumId=
[IndexFormat]=%_url%|%_preview%|%Album%|%Artist%|%Title%|%Released%|%Tracks%
[SearchBy]=Album||%album%||%s||Albumartist||%albumArtist%||+%s

[ParserScriptIndex]=...
# ###################################################################
# This script parses the webpage for all releases
# by this albumname.
#
# The output format is defined by IndexFormat above, which
# is used by the selection dialog
# ###################################################################
#debug "on" "debug_melon_search_album.txt"
#debugWriteInput "debug-input_melon_search_album.txt"

FindLine "<li class=\"album11_li\""

Do
  # _url
  FindLine "<dt"
  FindLine "goAlbumDetail"
  Unspace
  FindInLine "goAlbumDetail('"
  SayUntil "');"
  Say "|"

  # _preview
  Say "https://www.melon.com/album/detail.htm?albumId="
  Unspace
  FindInLine "goAlbumDetail('"
  SayUntil "');"
  Say "|"

  # Album
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"

  # Artist/Multiple Artists/Various Artists
  FindLine "atistname"
  FindLine "<div"
  JoinUntil "</div>"
  RegexpReplace ".*<span" "<span"
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"

  # Title
  FindLine "btn_play"
  FindLine "songname12"
  Unspace
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"

  # Released
  FindLine "cnt_view"
  Unspace
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"

  # Tracks
  FindLine "tot_song"
  Unspace
  RegexpReplace "<[^>]+>" ""
  SayRegexp "[0-9]+"
  SayNewLine

  FindLine "<li class=\"album11_li\">" 1 1
  Unspace
While "<li class=\"album11_li\""

[ParserScriptAlbum]=...
# ###################################################################
# This script parses the albums page for all common information.
#
# The current output variable is set via the outputto command
# All these fields are used by the confirm online information dialog
# ###################################################################
#debug "on" "debug_melon_parse_album.txt"
#debugWriteInput "debug-input_melon_parse_album.txt"

# CoverArt
OutputTo "coverurl"
FindLine "<meta property=\"og:image\""
Unspace

# ChangeSize
RegexpReplace "(_)(\d+)(.jpg+)" "_1000.jpg"
RegexpReplace "(\w+\.\w+)\?.*" "$1"
FindInLine "content=\""
SayUntil "\""

# Album
OutputTo "Album"
FindLine "<strong class=\"none\">"
MoveLine 1
Unspace
SayRest

# AlbumArtist
OutputTo "AlbumArtist"
FindLine "<div class=\""
FindInLine "<div class=\""
MoveLine 2
Unspace
RegexpReplace "<[^>]+>" ""
SayRest

OutputTo "Year"
FindLine "<dt>발매일</dt>"
MoveLine 1
Unspace
# Year
RegexpReplace "<[^>]+>" ""
SayUntil "."
# Date
#RegexpReplace "[^0-9]" ""
#SayRest

# Genre
#OutputTo "Genre"
#FindLine "<dt>장르</dt>"
#MoveLine 1
#Unspace
#RegexpReplace "<[^>]+>" ""
#SayRest

# Publisher
OutputTo "Publisher"
FindLine "<dt>발매사</dt>"
MoveLine 1
Unspace
RegexpReplace "<[^>]+>" ""
SayRest

FindLine "<tr data-group-items=\""
Unspace
Do
  # DiscNumber
  FindInLine "<tr data-group-items=\"cd"
  OutputTo "discTemp"
  SayUntil "\""
  Say "|"

  # Track
  OutputTo "trackTemp"
  FindLine "<span class=\"rank"
  Unspace
  RegexpReplace "<[^>]+>" ""
  SayUntil "위"
  Say "|"

  # Title
  OutputTo "titleTemp"
  FindLine "<div class="wrap_song_info">"
  FindLine "<div class="ellipsis">"
  FindLine "</span></div><br>"
  MoveLine -3
  FindLine "<"
  Unspace

  # Unplayable song
  if "<span class=\"disabled"
    RegexpReplace "<[^>]+>" ""
    SayRest
  else
    # Playable song
    if "<a href=\"javascript:melon.play.playSong"
      RegexpReplace "<[^>]+>" ""
      SayRest
    endif
  endif
  Say "|"

  # Artist
  OutputTo "artistTemp"
  FindLine "div class=\"ellipsis rank02\""
  FindLine "<span class=\"checkEllipsis\""
  Unspace
  KillTag "a" ""
  KillTag "/a" ""
  KillTag "span" "<"
  KillTag "/span" ">"
  Replace " , " ","
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"

  OutputTo "tracksTemp"
  Say "|"
  FindLine "<tr data-group-items=\"" 1 1
  Unspace
While "<tr data-group-items=\""

OutputTo "Track"
SayOutput "trackTemp"
OutputTo "Title"
SayOutput "titleTemp"
OutputTo "Tracks"
SayOutput "tracksTemp"
OutputTo "DISCNUMBER"
SayOutput "discTemp"
OutputTo "Artist"
SayOutput "artistTemp"

Set "trackTemp"
Set "titleTemp"
Set "tracksTemp"
Set "discFlag"
Set "discTemp"
Set "artistTemp"