[Include]=Melon#.inc
[IndexUrl]=https://www.melon.com/search/album/index.htm?section=album&searchGnbYn=Y&kkoSpl=N&kkoDpType=&sort=weight&q=
[AlbumUrl]=https://www.melon.com/album/detail.htm?albumId=
[IndexFormat]=%_url%|%_preview%|%Album%|%Artist%|%Title%|%Released%|%Tracks%
[SearchBy]=Album||%album%||%s

[ParserScriptIndex]=...
# ###################################################################
# This script parses the webpage for all releases
# by this albumname.
#
# The output format is defined by IndexFormat above, which
# is used by the selection dialog
# ###################################################################
#debug "on" "debug_melon_search_album.txt"
#debugWriteInput "debug-input_melon_search_album.html"

FindLine "<li class=\"album11_li\""
Do
  # _url
  FindLine "<dt"
  FindLine "goAlbumDetail"
  Unspace
  FindInLine "goAlbumDetail('"
  SayUntil "');"
  Say "|"

  # _preview
  Say "https://www.melon.com/album/detail.htm?albumId="
  Unspace
  FindInLine "goAlbumDetail('"
  SayUntil "');"
  Say "|"

  # Album
  RegexpReplace "<[^>]+>" ""
  SayRest
  Say "|"
  
  # Artist/Multiple Artists/Various Artists
  FindLine "atistname"
  FindLine "<div"
  JoinUntil "</div>"
  RegexpReplace "<span.*" ""
  RegexpReplace "<[^>]+>" ""
  Unspace
  SayRest
  Say "|"

  # Title
  FindLine "btn_play"
  FindLine "songname12"
  RegexpReplace "<[^>]+>" ""
  Unspace
  SayRest
  Say "|"

  # Released
  FindLine "cnt_view"
  RegexpReplace "<[^>]+>" ""
  Unspace
  SayRest
  Say "|"

  # Tracks
  FindLine "tot_song"
  RegexpReplace "<[^>]+>" ""
  Unspace
  SayRegexp "[0-9]+"
  SayNewLine

  FindLine "<li class=\"album11_li\">" 1 1
  Unspace
While "<li class=\"album11_li\""

[ParserScriptAlbum]=...
# ###################################################################
# This script parses the albums page for all common information.
#
# The current output variable is set via the outputto command
# All these fields are used by the confirm online information dialog
# ###################################################################
#debug "on" "debug_melon_parse_album.txt"
#debugWriteInput "debug-input_melon_parse_album.html"

# CoverArt
FindLine "<meta property=\"og:image\""

# ChangeSize
RegexpReplace "(_)(\d+)(.jpg+)" "_1000.jpg"
#RegexpReplace "(\w+\.\w+)\?.*" "$1"
Unspace
FindInLine "content=\""
OutputTo "coverurl"
SayUntil "\""

# Album
FindLine "song_name"
JoinUntil "</div>"
Replace "<strong class=\"none\">앨범명</strong>" ""
RegexpReplace "<[^>]+>" ""
Unspace
OutputTo "Album"
SayRest

# AlbumArtist/Multiple Artists/Various Artists
FindLine "artist"
JoinUntil "</div>"
RegexpReplace "<[^>]+>" ""
Unspace
OutputTo "AlbumArtist"
SayRest

FindLine "<dt>발매일</dt>"
FindLine "<dd"
Unspace
IfNot "-"
  OutputTo "Year"
  # Year
  RegexpReplace "<[^>]+>" ""
  SayUntil "."
  # Date
  #RegexpReplace "[^0-9]" ""
  #SayRest
endif

# Publisher
FindLine "<dt>발매사</dt>"
FindLine "<dd"
RegexpReplace "<[^>]+>" ""
Unspace
IfNot "-"
  OutputTo "Publisher"
  SayRest
endif

FindLine "<tr data-group-items=\""
Unspace
Do
  # DiscNumber
  FindInLine "<tr data-group-items=\"cd"
  OutputTo "discTemp"
  SayUntil "\""
  Say "|"

  # Track
  FindLine "<span class=\"rank"
  RegexpReplace "<[^>]+>" ""
  Unspace
  OutputTo "trackTemp"
  SayUntil "위"
  Say "|"

  # Title
  FindLine "wrap_song_info"
  JoinUntil "</div>"
  Replace "<span class=\"none\">Title</span>" ""
  RegexpReplace "<[^>]+>" ""
  Unspace
  OutputTo "titleTemp"
  SayRest
  Say "|"

  # Artist/Multiple Artists/Various Artists
  FindLine "ellipsis rank02"
  JoinUntil "</div>"
  RegexpReplace "<span.*" ""
  RegexpReplace "<[^>]+>" ""
  Unspace
  OutputTo "artistTemp"
  SayRest
  Say "|"

  OutputTo "tracksTemp"
  Say "|"
  FindLine "<tr data-group-items=\"" 1 1
  Unspace
While "<tr data-group-items=\""

OutputTo "DISCNUMBER"
SayOutput "discTemp"
OutputTo "Track"
SayOutput "trackTemp"
OutputTo "Title"
SayOutput "titleTemp"
OutputTo "Artist"
SayOutput "artistTemp"
OutputTo "Tracks"
SayOutput "tracksTemp"

Set "trackTemp"
Set "titleTemp"
Set "tracksTemp"
Set "discTemp"
Set "artistTemp"